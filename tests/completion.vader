Include: include/setup.vader

Execute (neomake#CompleteMakers: completes makers for b:):
  new
  let b:neomake_c_lint_maker = {
    \ 'exe': 'lint',
    \ 'args': ['--option', 'x'],
    \ 'errorformat': '%f:%l:%c: %m',
    \ }
  set filetype=c
  Assert index(neomake#CompleteMakers('', 'Neomake '), 'lint') != -1, 'lint maker gets completed'
  bwipe

Execute (neomake#CompleteMakers: completes makers for g:):
  Save g:neomake_c_lint_maker
  new
  let g:neomake_c_lint_maker = {
    \ 'exe': 'lint',
    \ 'args': ['--option', 'x'],
    \ 'errorformat': '%f:%l:%c: %m',
    \ }
  set filetype=c
  Assert index(neomake#CompleteMakers('', 'Neomake '), 'lint') != -1, 'lint maker gets completed'
  bwipe

Execute (neomake#CompleteMakers: completes space after Neomake!):
  AssertEqual neomake#CompleteMakers('', 'Neomake!'), [' ']

Execute (neomake#CompleteMakers: completes project makers):
  AssertEqual sort(neomake#CompleteMakers('', 'Neomake! ')),
  \ ['cabal', 'clippy', 'gradle', 'mvn', 'sbt']

Execute (neomake#CompleteJobs):
  if NeomakeAsyncTestsSetup()
    AssertEqual neomake#CompleteJobs(), ''
    let job_ids = [neomake#Sh('sleep 0.1')]
    AssertEqual neomake#CompleteJobs(), job_ids[0].': sh: sleep 0.1'
    let job_ids += [neomake#Sh('sleep 0.11')]
    AssertEqual neomake#CompleteJobs(), printf(
      \ "%d: sh: sleep 0.1\n%d: sh: sleep 0.11", job_ids[0], job_ids[1])
    NeomakeCancelJobs
    NeomakeTestsWaitForFinishedJobs
  endif

Execute (neomake#CompleteMakers: completes all makers, preferring filetype):
  new
  noautocmd set ft=rust

  " Neomake! completes ft makers (first), and project makers.
  AssertEqual neomake#CompleteMakers('', 'Neomake! '), [
  \ 'cargo', 'rustc', 'cabal', 'clippy', 'gradle', 'mvn', 'sbt']

  " Neomake completes ft makers.
  AssertEqual neomake#CompleteMakers('', 'Neomake '), [
  \ 'cargo', 'rustc']

  " Neomake completes ft makers with project makers when called again.
  AssertEqual neomake#CompleteMakers('', 'Neomake '), [
  \ 'cargo', 'rustc', 'cabal', 'clippy', 'gradle', 'mvn', 'sbt']

  " Neomake with ArgLead completes ft makers.
  AssertEqual neomake#CompleteMakers('c', 'Neomake c'), [
  \ 'cargo']

  " Neomake with ArgLead completes ft makers with project makers when called again.
  AssertEqual neomake#CompleteMakers('c', 'Neomake c'), [
  \ 'cargo', 'cabal', 'clippy']

  " Neomake completes global makers when no filetype makers match.
  AssertEqual neomake#CompleteMakers('g', 'Neomake g'), [
  \ 'gradle']

  " Neomake with ArgLead completes ft makers only again afterwards.
  AssertEqual neomake#CompleteMakers('c', 'Neomake c'), [
  \ 'cargo']

  bwipe
